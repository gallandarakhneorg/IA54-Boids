package fr.utbm.boids.util

class Vector {
	/** première dimension du vecteur */
	var x : double
	/** Deuxième dimension du vecteur */
	var y : double
	/** Troisième dimension du vecteur */
	var z : double
	
	/** 
	 * Cration d'un vecteur 2D
	 * Par défaut la troisième dimension vaut 0
	 * @param x - La valeur de la première dimension du vecteur
	 * @param y - La valeur de la deuxième dimension du vecteur
	 */
	new (x : double, y : double){
		this.x = x
		this.y = y 
		this.z = 0
	}
	
	/**
	 * Création d'un vecteur 3D
	 * @param x - La valeur de la première dimension du vecteur
	 * @param y - La valeur de la deuxième dimension du vecteur
	 * @param z - La valeur de la troisième dimension du vecteur
	 */
	new (x: double, y : double, z : double){
		this.x = x
		this.y = y
		this.z = z
	}
	
	/**
	 * Création d'une instance de Vector par copie.
	 * @param v - Le vecteur à copier.
	 */
	new (v : Vector){
		this.x = v.getX
		this.y = v.getY
		this.z = v.getZ
	}
	
	/**
	 * Création d'un vecteur nul
	 * Par défaut, les valeurs des trois dimensions sont nuls.
	 */
	new (){
		this.x = 0
		this.y = 0
		this.z = 0
	}

	/** 
	 * Permet de remettre les valeurs des dimensions à 0 
	 */
	def clear() : void {
		this.x = 0
		this.y = 0
		this.z = 0
	}
	
	/**
	 * Permet de setter la valeur de la première dimension du vecteur
	 * @param x - La valeur de la première dimension à modifier
	 */
	def setX (x : double) : void{
		this.x = x
	}
	/**
	 * Permet de setter la valeur de la deuxième dimension du vecteur
	 * @param y - La valeur de la deuxième dimension à modifier
	 */
	def setY (y : double) : void{
		this.y = y
	}
	/** 
	 * Permet de setter la valeur de la troisième dimension du vecteur
	 * @param z - La valeur de la troisième dimension à modifier
	 */
	def setZ(z : double) : void {
		this.z = z
	}
	
	/**
	 * Permet de setter en une fois la valeur de la première et deuxième dimension du vecteur
	 * @param x - La valeur de la première dimension à modifier
	 * @param y - La valeur de la deuxième dimension à modifier
	 */
	def setXY(x : double, y :double) : void{
		this.x = x
		this.y = y
	}
	/**
	 * Permet de setter en une fois la valeur de la première et deuxième dimension du vecteur à l'aide d'un autre vecteur
	 * @param vec - Le vecteur duquel il faut récupérer les informations
	 */
	def setXY (vec : Vector) : void {
		this.x = vec.getX()
		this.y = vec.getY()
	}
	
	/**
	 * @return La valeur de la première dimension du vecteur
	 */
	def getX () : double  {
		return x
	}
	
	/**
	 * @return La valeur de la deuxième dimension du vecteur
	 */
	def getY() : double{
		return y
	}
	
	/** 
	 * @return La valeur de la troisième dimension du vecteur
	 */
	def getZ() : double{
		return this.z
	}
	
	/**
	 * Permet la soustraction d'un vecteur à notre vecteur
	 * @param vec - Le vecteurà soustraire
	 */
	def moins (vec : Vector){
		this.x = this.x - vec.x
		this.y = this.y - vec.y 
		this.z = this.z - vec.z
	}

	/**
	 * Permet l'addition d'un vecteur à notre vecteur
	 * @param vec - Le vecteur à additioner
	 */
	def plus(vec : Vector) {
		this.x = this.x + vec.x
		this.y = this.y + vec.y
		this.z = this.z + vec.z
	}

	/**
	 * Permet de multiplier notre vecteur par un réel
	 * @param a Le réel par lequel notre vecteur va être multiplié
	 */
	def fois(a : double) {
		this.x = x * a
		this.y = y * a
		this.z = z * a
	}
	
	/**
	 * Permet de calculer la longueur de notre vecteur
	 * @return La longueur de notre vecteur
	 */
	def length () : double {
		return Math.sqrt (x*x + y*y + z*z)
	}
	
	/**
	 * Permet de normaliser notre vecteur, i.e d'avoir une longueur de 1
	 */
	def normaliser() {
		var longueur = this.length
		if (longueur > 0){
			this.x = this.x / longueur
			this.y = this.y / longueur
			this.z = this.z / longueur
		}
	}
	
	/**
	 * Effectuer le produit scalaire de notre vecteur avec un autre vecteur
	 * @param vec - Le vecteur avec lequel on effectue le produit scalaire
	 * @return La valeur du produit scalaire
	 */
	def point (vec : Vector) : double{
		return (this.x*vec.x + this.y*vec.y + this.z*vec.z)
	}
	
	/**
	 * Permet de calculer le produit vectoriel de notre vecteur avec un second vecteur
	 * @param vec - Le vecteur avec lequel on effectue le produit vectoriel
	 * @return Le vecteur résultant de notre produit vectoriel
	 */
	def produitVectoriel (vec : Vector) : Vector{
		var produit : Vector
		produit = new Vector(0,0)
		produit.setX(this.y * vec.z - this.z * vec.y)
		produit.setY(this.z * vec.x - this.x * vec.z)
		produit.setZ(this.x * vec.y - this.y * vec.x)
		return produit
		
	}
	
	/**
	 * Permet de transformer un vecteur en une chaîne de caractères
	 * @return La chaîne de caractères correpsondant à l'instance de la classe Vector
	 */
	def toString() : String{
		return "x : " + this.x + " ,y : " + this.y + " ,z : " + this.z + "   " 
	}
	
	
}